plugins {
    id "java"
    id "java-library"
    id "io.freefair.lombok" version "6.5.0.3"
    id "maven-publish"
}

group 'com.cadiducho'
version '1.5.1'

java {
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            name = "cadiduchoRepoSnapshots"
            url = "https://repo.cadiducho.com/snapshots"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            name = "releases"
            url = "https://repo.cadiducho.com/releases"
            credentials {
                username System.getenv("MAVEN_NAME")
                password System.getenv("MAVEN_PASSWORD")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
}

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://repo.cadiducho.com/releases"
    }
}

dependencies {
    api group: 'com.cadiducho', name: 'TelegramBotAPI', version: '6.2.1'

    //Tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.7.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.7.0'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}